! ********** PREAMBLE SECTION **********
Title "MPM Analysis"
Name "User Name"

! Header
Header
Two disks or blocks with normal impact
The contact is handle my multimaterial MPM methgods
EndHeader

! Analysis can be "Plane Stress MPM", "Plane Strain MPM", or "3D MPM"
#analysis$="3D MPM"

! Strain update method "USAVG" or "USF"
#update$="USAVG+"

! Shape functions "uGIMP" or "lCPDI"
#method$="uGIMP"

! "yes" or "no" to use contact in multimaterial mode
#contact$="yes"

! ********** PARAMTERS SECTION **********
! dimensions (width and height multiple of cell)
#cell=5

! analysis space
#width=200
#height=60
#sepcells=6

! left ball or block
#lwidth=50
#lheight=50
#linit=0.07			! fraction of wave speed left

! right ball or block
#rwidth=50
#rheight=50
#rinit=0.07			! fraction of wave speed right

#thick=1
#shape$="Round"			! "Round" or "Square" for disks/spheres or blocks

! Disk properties
#Edisk=1000			! E in MPa
#rhodisk=1.5			! density in g/cm^3
#model$="Neo"			! "Hypo" or "Neo" for hypoelastic or neohookean

!-------------- Begin Calculations ---------------------

Analysis #analysis$

! calculate wave speed and impact at some fraction of that speed
#vwave=1000*sqrt(1000*#Edisk/#rhodisk)
#wspd=#vwave/1000
Comment "E = "&#Edisk&" MPa, rho = "&#rhodisk&" g/cm^3, wave spd = "&#wspd&" m/sec"

! from separation, find disk lower-left origins
if #sepcells!=2*int(#sepcells/2)
  #sepleft=(#sepcells-1)*#cell
  #sepright=(#sepcells+1)*#cell
else
  #sepleft=#sepcells*#cell
  #sepright=#sepcells*#cell
endif
#lx=(#width-#sepleft)/2-#lwidth
#rx=(#width+#sepright)/2
#ly=int((#height-#lheight)/(2*#cell))*#cell
#ry=int((#height-#rheight)/(2*#cell))*#cell

#lspeed=#linit*#vwave		    ! speed in mm/s left
#rspeed=#rinit*#vwave		    ! speed in mm/s right
Comment "Left speed = "&(#lspeed/1000)&" m/sec"
Comment "Right speed = "&(#rspeed/1000)&" m/sec"

! estimate time to contact and bounce off
! assume masses or the same, they then exchange velocites
#travel=#sepcells*#cell+#width/4
#mxtime=1000*#travel/(#lspeed+#rspeed)	    ! total time in ms

MPMmethod #update$,#method$
if #analysis$="3D MPM"
  Archive "Results/"&#shape$&"3D/"&#model$&"/"&#update$&"-"&#method$
else
  Archive "Results/"&#shape$&"/"&#model$&"/"&#update$&"-"&#method$
endif
ArchiveTime #mxtime/50
MaximumTime #mxtime
ToArchive velocity,stress,strain,strainenergy,rotstrain

! Use contact?
if #contact$="yes"
  MultimaterialMode 0,"enabled","avggrad"
  ContactPosition 0.8
endif

! ********** MATERIALS SECTION **********
if #model$="Hypo"
  Material "Left","Polymer Left",1
    E #Edisk
    nu .33
    alpha 60
    rho #rhodisk
  Done
  Material "Right","Polymer Right",1
    E #Edisk
    nu .33
    alpha 60
    rho #rhodisk
  Done
else
  Material "Left","IPolymer","Mooney"
    K #Edisk/(3*(1-2*.33))
    G1 #Edisk/(2*(1+.33))
    alpha 60
    rho #rhodisk
  Done
  Material "Right","IPolymer","Mooney"
    K #Edisk/(3*(1-2*.33))
    G1 #Edisk/(2*(1+.33))
    alpha 60
    rho #rhodisk
  Done
endif

GlobalArchiveTime #mxtime/100
GlobalArchive "sxx","Left"
GlobalArchive "sxx","Right"
GlobalArchive "velx","Left"
GlobalArchive "velx","Right"

! ********** GRID SECTION **********
GridHoriz #width/#cell,0,-1,#width
GridVert #height/#cell,0,-1,#height
if #analysis$="3D MPM"
  GridDepth #height/#cell,0,-1,#height
  GridRect 0,#width,0,#height,0,#height
  
  Region "Left",#lspeed,0,0
    if #shape$="Round"
      Sphere #lx,#lx+#lwidth,#ly,#ly+#lheight,#ly,#ly+#lheight
    else
      Rect #lx,#lx+#lwidth,#ly,#ly+#lheight,#ly,#ly+#lheight
    endif
  EndRegion
  
  Region "Right",-#rspeed,0,#thick
    if #shape$="Round"
      Sphere #rx,#rx+#rwidth,#ry,#ry+#rheight,#ry,#ry+#rheight
    else
      Rect #rx,#rx+#rwidth,#ry,#ry+#rheight,#ry,#ry+#rheight
    endif
  EndRegion
else

  GridThickness #thick
  GridRect 0,#width,0,#height
  
  Region "Left",#lspeed,0,#thick
    if #shape$="Round"
      Oval #lx,#lx+#lwidth,#ly,#ly+#lheight
    else
      Rect #lx,#lx+#lwidth,#ly,#ly+#lheight
    endif
  EndRegion
  
  Region "Right",-#rspeed,0,#thick
    if #shape$="Round"
      Oval #rx,#rx+#rwidth,#ry,#ry+#rheight
    else
      Rect #rx,#rx+#rwidth,#ry,#ry+#rheight
    endif
  EndRegion
endif