! ********** Introduction **********

! ********** Preamble Section **********
Title "3D Tensile Test"
Name "John Nairn"

! Header
Header
   MPM Analysis using NairnFEAMPM and NairnMPM
EndHeader

! "Plane Strain MPM" or "3D MPM"
#analysis$="Plane Strain MPM"
Analysis #analysis$
MPMMethod USF,lCPDI

! ********** Parameters Section **********
#matID$="idealgas"		! can be "idealgas" or "liquid"
#strain=0				! strain
#room=50				! extra strain room for thermal expansion
#tmult=100

#cell=5                 ! cell size (cubic cells)
#ywidth=40              ! width in the y direction (mult of cell)
#zwidth=40              ! width in the z direction (mult of cell)
#length=100             ! Length in x direction (mult of cell)

StressFreeTemp 288.15
#startTemp=288.15

! calculate wave speed (mm/sec)
if #matID$="idealgas"
  #P0=.101
  #T0=288.15
  #rho0=0.001164
  #speed=1000000*sqrt(5000*#P0*#startTemp*#rho0/(3*#T0))
else
  #Kbulk=2200
  #rho0=1
  #speed=1000*sqrt(1000*#Kbulk/#rho0)	     
endif
#transit=1000*#length/#speed             ! transit time in ms
#time=#tmult*#transit                    ! time to peak load in ms
#disp=#strain*#length/100                ! mm of displacement
#disprate=1000*#disp/#time               ! displacement rate mm/s

#totalHeat=8							 ! total heat applied as flux on the edges
#constP=#P0							 ! <0 to ignore
conduction "Yes","Adiabatic"

! Archiving
Archive "Results/"&#matID$
TimeStep 1000,#time,.05
ArchiveTime #time/40
ToArchive velocity,stress,strain,strainenergy,elementcrossings,rotStrain,temperature,history1,heatenergy

! ********** Materials Section **********
if #matID$="idealgas"
  Material #matID$,"Ideal Gas","idealgas"
    rho #rho0
    P0 #P0
    T0 #T0
    kCond 300
  Done
endif

XMLData Material,"liquid"
  <Material Type="27" Name="Water">
    <K>2200</K>
    <viscosity>1</viscosity>
    <rho>1</rho>
    <kCond>0</kCond>
    <Cv>418.13</Cv>
    <alpha>70</alpha>
  </Material>
EndXMLData

Material "rigid","Loading Clamp","Rigid"
  direction 1
  SettingFunction #disprate
  mirrored 1
Done

Material "floor","Wall","Rigid"
  direction 1
  mirrored -1
Done

Material "wally","Wall","Rigid"
  direction 2
Done

Material "wallz","Wall","Rigid"
  direction 4
Done

if #constP>0
  Material "plug","Isotropic","Isotropic"
    E 1
    nu .33
    alpha 0
    rho .1
  Done
endif

! global archive
GlobalArchive sxx,#matID$
GlobalArchive exx,#matID$
GlobalArchive "Strain Energy",#matID$
GlobalArchive "Entropy",#matID$
GlobalArchive "Heat Energy",#matID$
GlobalArchive "Work Energy",#matID$
GlobalArchive "Internal Energy",#matID$
GlobalArchiveTime #time/100

! ********** Grid and Material Points Section **********

! Extra length for pulling (mult of cell)
if #strain<0
  #excess=int(#room*#length/(100*#cell)+2)*#cell
else
  #excess=int((#strain+#room)*#length/(100*#cell)+2)*#cell
endif

#ycells=int(#ywidth/#cell+.5)+4
#zcells=int(#ywidth/#cell+.5)+4
#xcells=int((#length+#excess)/#cell+.5)+1
GridHoriz #xcells
GridVert #ycells
if #analysis$="3D MPM"
  GridDepth #zcells
  GridRect -#cell,#length+#excess,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell

  Region #matID$,0,0,0,"temp",#startTemp
    Box 0,#length,0,#ywidth,0,#zwidth
  EndRegion

  Region "floor",0,0,0
    Box -#cell,0,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell
  EndRegion

  Region "wally",0,0,0
    Box 0,#length,-#cell,0,-#cell,#zwidth+#cell
    Box 0,#length,#ywidth,#ywidth+#cell,-#cell,#zwidth+#cell
  EndRegion
  Region "wallz",0,0,0
    Box 0,#length,0,#ywidth,-#cell,0
    Box 0,#length,0,#ywidth,#zwidth,#zwidth+#cell
  EndRegion

  Region "rigid",1,0,0
    Box #length,#length+#cell,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell
  EndRegion

else
  GridThickness #zwidth
  GridRect -#cell,#length+#excess,-2*#cell,#ywidth+2*#cell

  Region #matID$,0,0,#zwidth,"temp",#startTemp
    Rect 0,#length,0,#ywidth
  EndRegion

  Region "floor",0,0, #zwidth
    Rect -#cell,0,-2*#cell,#ywidth+2*#cell
  EndRegion

  Region "wally",0,0, #zwidth
    Rect 0,#length+#excess,-#cell,0
    Rect 0,#length+#excess,#ywidth,#ywidth+#cell
  EndRegion

  if #constP>0
    Region "plug",0,0,#zwidth
      Rect #length,#length+2*#cell,0,#ywidth
    EndRegion
    LoadLine #length+2*#cell,#cell/2,#length+2*#cell,#ywidth-#cell/2,#cell/2
      Traction "x",2,"constant",-0.101
    EndLoadLine
  else
    Region "rigid",1,0, #zwidth
      Rect #length,#length+#cell,-2*#cell,#ywidth+2*#cell
    EndRegion
  endif
  
  if #totalHeat>0
     #area=2*(#length+#ywidth)*#zwidth/1e6
  	 #dQ=1000*#totalHeat/(#time*#area)
     LoadLine #cell/4,0,#cell/4,#ywidth
       HeatFlux "external",4,"constant",#dQ
     EndLoadLine
     LoadLine #length-#cell/4,0,#length-#cell/4,#ywidth
       HeatFlux "external",2,"constant",#dQ
     EndLoadLine
     LoadLine 0,#cell/4,#length,#cell/4
       HeatFlux "external",1,"constant",#dQ
     EndLoadLine
     LoadLine 0,#ywidth-#cell/4,#length,#ywidth-#cell/4
       HeatFlux "external",3,"constant",#dQ
     EndLoadLine
  endif
endif

CustomTask AdjustTimeStep
Parameter verbose,1