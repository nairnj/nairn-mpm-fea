! ********** Introduction **********

! ********** Preamble Section **********
Title "Tension Test"
Name "John Nairn"

! Header
Header
   This file has sample of all elastic materials in NairnFEA.
   You can try eacj material with any supported element type as well
   For anisotropic materials, you can set a rotation angle
EndHeader

! options are "Plane Stress", "Plane Strain", or "Axisymmetric"
#mode$="Plane Stress"

! Element type
#elem$="8 Node Quadrilateral"

! Material types I"sotropic", "Trans Iso IP", "Trans Iso N","Ortho"
#matID$="Isotropic"
#angle=0

! Specimen
#length=100
#width=20
#thick=1
#rotate=0

! Mesh
#axial=10
#trans=4

! Loads "displacement" (by #stress MPa),"force" (by #force N, total),
!             or "displacement" (by #disp mm)
#loading$="force"
#stress=100
#force=100*#width*#thick
#disp=0.01*#length

Analysis #mode$
Element #elem$

! ********** Parameters Section **********
#matname="polymer"

! ********** Materials Section **********
if #matID$="Isotropic"
  Material #matID$,"Polymer","Isotropic"
    E 2300
    nu .33
    a 60
  Done
endif

! ********** Meshing Section **********

! Keypoints, which might be rotated cw
! (x,y) -> (x cos t + y sin t, -x sin t + y cos t)
#rot=3.14159286*#rotate/180
Keypoint "BL",0,0

! rotate (#length,0)
Keypoint "BR",#length*cos(#rot),-#length*sin(#rot)

! rotate (#length,#width)
Keypoint "TR",#length*cos(#rot)+#width*sin(#rot),-#length*sin(#rot)+#width*cos(#rot)

! rotate (0,#width)
Keypoint "TL",#width*sin(#rot),#width*cos(#rot)

Area #matID$,#thick,#angle
  Path "Left",#trans
    Keypoints "TL","BL"
  EndPath
  Path "Bottom",#axial
    Keypoints "BL","BR"
  EndPath
  Path "Right",#trans
    Keypoints "BR","TR"
  EndPath
  Path "Top",#axial
    Keypoints "TR","TL"
  EndPath
EndArea

! ********** Boundary Condition Section **********
FixLine "Left"
  Rotate "z",#rotate
  Displacement x
EndFixLine

FixPoint "BL"
  Displacement y
EndFixPoint

if #loading$="stress"
  FixLine "Right"
    Stress "n",#stress
  EndFixLine

else if #loading$="force"
  if #elem$="4 Node Quadrilateral"
    #nodes=#trans+1
  else if #elem$="3 Node Triangle"
    #nodes=#trans+1
  else
    #nodes=2*#trans+1
  endif
  FixLine "Right"
    Rotate "z",#rotate
    Load "x",#force/#nodes			! N per node
  EndFixLine

else if #loading$="displacement"
  FixLine "Right"
    Rotate "z",#rotate
    Displacement "x",#disp
  EndFixLine
 
else
  Stop "The #loading$ parameter is not recognized"
  
endif

! ********** Miscellaneous **********
Resequence "BL"
