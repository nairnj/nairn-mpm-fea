! ********** Preamble Section **********
Title "FEA Analysis of Cantilever Beam with an Exact Solution"
Name "John Nairn"

! Header
Header
A cantilever beam that is fixed on a few points at its base to implement
    Timoshenko and Goodier displacements, loaded on free
    end by shear traction, and loaded on fixed edge by shear and normal traction
    to match stress functions used by Timoshenko and Goodier.
The output should exactly match Timoshenko and Goodier stress function
    stresses and displacements
See Zienkiewicz, Taylor, and Zhu, The FEM, Its Basis and Fundamentals, Page 41
The exact total energy should be 2.575e-4 * P^2
    where P is specified in #load
EndHeader

Analysis "Plane Stress FEA"
Element "8 Node Quadrilateral"
Processors 4

! ********** Parameters Section **********
! Define variables for more general mesh generation capabilities
#length=100               ! Length in mm
#c=10                     ! Semi-depth in mm
#thick=1                  ! Thickness in mm
#load=40                  ! End load in N
#matname$="polymer"       ! which material to use
#energy=0.0002575*#load*#load
Comment "Total energy = "&#energy&" J"

! Increasing #scale will increase the number of elements
#scale=1
#horiz=10*#scale          ! Elements in horizontal direction
#vert=4*#scale            ! Element in vertical direction

! ********** Materials Section **********
Material "polymer","Polymer","Isotropic"
  E 1000
  nu .25
  a 60
Done

! ********** Meshing Section **********
Area #matname$,#thick
  Path "Bottom",#horiz
    Keypoint "BotLeft",0,-#c
    Keypoint "BotRight",#length,-#c
  EndPath
  Path "Right",#vert
    Keypoint "BotRight"
    Keypoint "TopRight",#length,#c
  EndPath
  Path "Top",#horiz
    Keypoint "TopRight"
    Keypoint "TopLeft",0,#c
  EndPath
  Path "Left",#vert
    Keypoint "TopLeft"
	Keypoint "BotLeft"
  EndPath
EndArea

! ********** Boundary Condition Section **********
FixPoint #length,0
  Displacement x
  Displacement y
EndFixPoint

FixPoint #length,-#c
  Displacement x
EndFixPoint

FixPoint #length,#c
  Displacement x
EndFixPoint

#shearmid = 0.75*#load/#c
#normtop = -1.5*#load*#length/(#c*#c)

! quadratic shear on free end: -#shearmid in the middle and zero on surfaces
FixLine "Left"
  Stress "t",0,-#shearmid,0
EndFixLine

! quadratic shear on fixed end: -#shearmid in the middle and zero on surfaces
! linear normal stress on fixed end
FixLine "Right"
  Stress "t",0,-#shearmid,0
  Stress "n",-#normtop,#normtop
EndFixLine

Resequence "BotLeft"
Output "forces","no","elementstresses","no"
