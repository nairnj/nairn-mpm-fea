! ********** Preamble Section **********
Title "FEA Analysis of Cantilever Beam"
Name "A NairnFEAMPM User"

! Header
Header
  A cantilever beam that is clamped at its base and
  loaded on its free end with a single point load.
EndHeader

Analysis "Plane Stress FEA"
Element "8 Node Quadrilateral"
Processors 2

! ********** Parameters Section **********
! Define variables for more general mesh generation capabilities
#length=50               ! Length in mm
#depth=10                ! Depth in mm
#thick=10                ! Thickness in mm
#horiz=6                 ! Elements in horizontal direction
#vert=3                  ! Element in vertical direction
#load=500                ! End load in N
#matname$="polymer"      ! which material to use

! ********** Materials Section **********
Material "polymer","Polymer","Isotropic"
  E 2300
  nu .33
  a 60
Done

! ********** Meshing Section **********
Area #matname$,#thick
  Path "Bottom",#horiz
    Keypoint "BotLeft",0,0
    Keypoint "BotRight",#length,0
  EndPath
  Path "Right",#vert
    Keypoints "BotRight"
    Keypoint "TopRight",#length,#depth
  EndPath
  Path "Top",#horiz
    Keypoints "TopRight"
    Keypoint "TopLeft",0,#depth
  EndPath
  Path "Left",#vert
    Keypoints "TopLeft","BotLeft"
  EndPath
EndArea
Resequence "BotRight"

! ********** Boundary Condition Section **********
FixLine 0,0,0,#depth
  Displacement x
  Displacement y
EndFixLine

FixPoint "TopRight"
  Load y,#load
EndFixPoint

