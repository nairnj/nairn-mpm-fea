! ********** Introduction **********

! ********** Preamble Section **********
Title "3D Tensile Test"
Name "John Nairn"

! Header
Header
   MPM Analysis using NairnFEAMPM and NairnMPM
EndHeader

! "Plane Strain MPM" or "3D MPM"
#analysis="Plane Strain MPM"
Analysis #analysis
MPMMethod USF,lCPDI

! ********** Parameters Section **********
#matID="idealgas"		! idealgas
#strain=-50				! strain
#tmult=20

#cell=5                 ! cell size (cubic cells)
#ywidth=40              ! width in the y direction (mult of cell)
#zwidth=40              ! width in the z direction (mult of cell)
#length=100             ! Length in x direction (mult of cell)

StressFreeTemp 300
#startTemp=300
!ThermalRamp 100,3
!conduction "Yes","Mechanical energy"

! calculate time to reach peak state
#P0=.101
#T0=288.15
#rho0=0.001164
#speed=1000000*sqrt(5000*#P0*#startTemp*#rho0/(3*#T0))
#transit=1000*#length/#speed             ! transit time in ms
#time=#tmult*#transit                    ! time to peak load in ms
#disp=#strain*#length/100                ! mm of displacement
#disprate=1000*#disp/#time               ! displacement rate mm/s

! Archiving
Archive "Results/"&#matID
TimeStep 1000,#time,.1
ArchiveTime #time/40
ToArchive velocity,stress,strain,strainenergy,elementcrossings,rotStrain,temperature,plasticenergy,plasticstrain

! ********** Materials Section **********
if #matID="idealgas"
  Material #matID,"Ideal Gas","idealgas"
    rho #rho0
    P0 #P0
    T0 #T0
  Done
endif

Material "rigid","Loading Clamp","Rigid"
  direction 1
  SettingFunction #disprate
Done

Material "floor","Wall","Rigid"
  direction 1
Done

Material "wally","Wall","Rigid"
  direction 2
Done

Material "wallz","Wall","Rigid"
  direction 4
Done

! global archive
GlobalArchive sxx,#matID
GlobalArchive exx,#matID
GlobalArchive "Strain Energy",#matID
GlobalArchive "Plastic Energy",#matID			! entropy here
GlobalArchiveTime #time/100


! ********** Grid and Material Points Section **********

! Extra length for pulling (mult of cell)
if #strain<0
  #excess=2*#cell
else
  #excess=int(#strain*#length/(100*#cell)+2)*#cell
endif

#ycells=int(#ywidth/#cell+.5)+4
#zcells=int(#ywidth/#cell+.5)+4
#xcells=int((#length+#excess)/#cell+.5)+1
GridHoriz #xcells
GridVert #ycells
if #analysis="3D MPM"
  GridDepth #zcells
  GridRect -#cell,#length+#excess,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell

  Region #matID,0,0,0,"temp",#startTemp
    Box 0,#length,0,#ywidth,0,#zwidth
  EndRegion

  Region "floor",0,0,0
    Box -#cell,0,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell
  EndRegion

  Region "wally",0,0,0
    Box 0,#length,-#cell,0,-#cell,#zwidth+#cell
    Box 0,#length,#ywidth,#ywidth+#cell,-#cell,#zwidth+#cell
  EndRegion
  Region "wallz",0,0,0
    Box 0,#length,0,#ywidth,-#cell,0
    Box 0,#length,0,#ywidth,#zwidth,#zwidth+#cell
  EndRegion

  Region "rigid",1,0,0
    Box #length,#length+#cell,-2*#cell,#ywidth+2*#cell,-2*#cell,#zwidth+2*#cell
  EndRegion

else
  GridThickness #zwidth
  GridRect -#cell,#length+#excess,-2*#cell,#ywidth+2*#cell

  Region #matID,0,0,#zwidth,"temp",#startTemp
    Rect 0,#length,0,#ywidth
  EndRegion

  Region "floor",0,0, #zwidth
    Rect -#cell,0,-2*#cell,#ywidth+2*#cell
  EndRegion

  Region "wally",0,0, #zwidth
    Rect 0,#length+#excess,-#cell,0
    Rect 0,#length+#excess,#ywidth,#ywidth+#cell
  EndRegion

  Region "rigid",1,0, #zwidth
    Rect #length,#length+#cell,-2*#cell,#ywidth+2*#cell
  EndRegion
endif

XMLData

  <CustomTasks>
    <Schedule name='AdjustTimeStep'>
      <Parameter name='verbose'>1</Parameter>
    </Schedule>
  </CustomTasks>
EndXMLData

