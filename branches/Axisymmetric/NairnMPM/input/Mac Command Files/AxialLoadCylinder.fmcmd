! ********** Introduction **********

! ********** Preamble Section **********
Title "MPM Analysis of Axially Loaded Cylinder"
Name "John A. Nairn"

! Header
Header
  A solid or hollow disk with internal and/or external
  pressure in axisymmetric MPM analysis
EndHeader

Analysis "Axisymmetric MPM"
#method="lCPDI"
MPMMethod USF,#method

! ********** Parameters Section **********
#length=50                  ! Length in mm
#Ri=0                   	! Internal radius
#Ro=10                   	! Thickness in mm

! "force" or "traction" to apply #stress on top
! "rigid" to extend to dL = #length*#ezzmax
#style="force"				
#stress=230
#ezzmax=0.1

#Ebase=2300
#rhobase=1.2
#matname="polymer"

#mult=250					! transit times for complete analysis
#rampfraction=0.5			! fraction to full pressure or strain

#cell=2.5
#border=2

! Calculate maximum and ramp times
#speed=sqrt(1000*#Ebase/#rhobase)		! in mm/msec
#transit=#length/#speed		! in msec
#maxtime=#mult*#transit
#ramp=#maxtime*#rampfraction

Archive "Results/Cyinder_"&#method&"_"&#style&"/ASMPM"
ArchiveTime #maxtime/25
MaximumTime #maxtime
!Damping 1500

GlobalArchive sxx
GlobalArchive exxe
GlobalArchive syy
GlobalArchive eyye
GlobalArchive szz
GlobalArchive ezze

! ********** Materials Section **********
Material "polymer","Polymer","Isotropic"
  E #Ebase
  nu .33
  a 60
  rho #rhobase
Done

if #style="rigid"
  #maxDisp = #length*#ezzmax
  #rate = #maxDisp/#ramp
  #rfxn = (1000*#rate)&"*(1-sign(t-"&#ramp&"))"
  Material "rigid","Rigid Piston","Rigid"
    direction 2
    SettingFunction #rfxn
  Done
else
  #maxDisp = #length*#stress/#Ebase
endif
if #maxDisp>0
  #extra=int(#maxDisp/#cell)+1+#border
else
  #extra=2
endif

! ********** Grid and Material Points Section **********

GridHoriz (#Ro+2*#border*#cell)/#cell
GridVert (#length+#border*#cell+#extra*#cell)/#cell
GridRect -#border*#cell,#Ro+#border*#cell,-#border*#cell,#length+#extra*#cell

Region #matname,0,0,1
  Rect #Ri,#Ro,0,#length
EndRegion

! ********** Boundary Conditions Section **********

! zero displacement at r=0
MoveLine -#cell/2,-#border*#cell,-#cell/2,#length+#extra*#cell,0.55*#cell
  Velocity x,constant,0
EndMoveLine

! To loading
if #style="rigid"
  Region "rigid",0,0,1
    Rect 0,#Ro+#border*#cell,#length,#length+#cell
  EndRegion
else
  LoadLine 0,#length,#Ro,#length,#cell/2
    if #style="force"
      LoadType net
      Load y,linear,#stress*#Ro*#Ro/(2*#ramp)
      Load y,linear,-#stress*#Ro*#Ro/(2*#ramp),#ramp
    else
      Traction y,3,linear,#stress/#ramp
      Traction y,3,linear,-#stress/#ramp,#ramp
    endif
  EndLoadLine
endif

! Bottom BC
MoveLine -#cell,-#cell/2,#Ro+#border*#cell,-#cell/2,0.55*#cell
  Velocity y,constant,0
EndMoveLine
